/********
    *
    * Advantage:
    *
    * In the following, a (`t`, `c`, `n`)-attacker is an attacker `A` that runs in time at most `t`,
    * makes at most `c` challenge queries, and never produces a group with more than `n` members.
    * The attacker wins the CGKA security game if they correctly guesses the random bit `b` in
    * the end and the safety predicate `P` evaluates to true on the queries made by the attacker.
    *
    * Hence we define our (`t`, `c` `n`) constants below:
    *
********/

// A constant for missing data
#define NONE 255

/****
  *
  * Security game's parameters' valid ranges:
  *   - T <- [3, 255]
  *   - C <- [1,   T]
  *   - N <- [3, 127]
  *
  * Lookup table for TREE value derived from N:
  *   - [ 1,   1] ->   1
  *   - [ 2,   2] ->   3
  *   - [ 3,   4] ->   7
  *   - [ 5,   8] ->  15
  *   - [ 9,  16] ->  31
  *   - [17,  32] ->  63
  *   - [33,  64] -> 127
  *   - [65, 128] -> 255
  *
****/
#define T    3
#define C    3
#define N    4
#define TREE 7
#define ROOT (TREE - 1)


/****
  *
  * Special types required for the protocol.
  *
****/


//        Void - This node does not exist  and there exists 0 leaves in the subtree
// UnknownRefs - This node does not exist  and there exists 1 leaf   in the subtree
//   KnownRefs - This node does not exist  and there exists 1 leaf   in the subtree
// UnknownNode - This node          exists and there exists 1 or more leaves in the subtree
//   KnownNode - This node          exists and there exists 1 or more leaves in the subtree
mtype:Secret = { Void, UnknownRefs, KnownRefs, UnknownNode, KnownNode };

mtype:Message = { ADD, RMV, UPD };

typedef Group      { bool         membership[N] };
typedef Enigma     { mtype:Secret secret[T]     };
typedef TreeKeys   { mtype:Secret node[ TREE ]  };


/****
  *
  * Global state of the CGKA security game.
  *
****/

byte    lead[T]; // Who initiated the epoch?
Group      G[T]; // Group membership by epoch
bool   chall[T]; // Has the attacker challenged in an epoch?
byte      ep[N]; // The epoch of each user
byte       D[N]; // Epoch from which the user saves secrets
bool  unsafe[N]; // Members which require a change to update
bool testmailed[N]; 

// Epoch, Sender
chan M[N] = [2] of { byte, byte };

TreeKeys attackerKnowledge[T];


/****
  *
  * Global (local) variable for inline functions.
  *
****/

local byte i = 0
local byte j = 0;
local byte t = 0;
local byte absentees = 0;
local byte attendees = N;
local byte unsafeIDs = 0;


/********
    *
    * Global state priniting utilities:
    *
    *   - print_challenges
    *   - print_leadership
    *   - print_membership
    *
********/


inline print_challenges()
{
    d_step {
        printf("\n\tChallenges:");
        for ( i : 0 .. T - 1)
        {
            if
            :: chall[i] -> printf("\n\t    [\tTrue\t]");
            :: else     -> printf("\n\t    [\tFalse\t]");
            fi
        }
        printf("\n");
    }
}


inline print_leadership()
{
    d_step {
        printf("\n\tLeadership:");
        for ( i : 0 .. T - 1)
        {
            if
            :: lead[i] == NONE -> printf("\n\t    [\tNONE\t]")
            :: else            -> printf("\n\t    [\t%d\t]", lead[i])
            fi
        }
        printf("\n");
    }
}


inline print_membership()
{
    d_step {
        byte e = 0;
        for ( i : 0 .. T - 1 )
        {
           if
           :: lead[i] != NONE -> e++
           :: else
           fi
        }
        
        printf("\n\tMembership:");
        for ( i : 0 .. e - 1 )
        {
            printf("\n\t  At: %d",i);
            for( j : 0 .. N - 1 )
            {
                if
                :: G[i].membership[j] -> printf("\n\t    [\tTrue\t]");
                :: else               -> printf("\n\t    [\tFalse\t]");
                fi
            };
        }
        printf("\n");
    }
}


inline print_user_epochs()
{
    d_step {
        printf("\n\tEpoch observing:");
        for ( i : 0 .. N - 1)
        {
            if
            :: ep[i] == NONE -> printf("\n\t    [\tNONE\t]")
            :: else          -> printf("\n\t    [\t%d\t]", ep[i])
            fi
        }
        printf("\n");
    }
}


inline print_user_hoarding()
{
    d_step {
        printf("\n\tHoarding since:");
        for ( i : 0 .. N - 1)
        {
            if
            :: D[i] == NONE -> printf("\n\t    [\tNONE\t]")
            :: else         -> printf("\n\t    [\t%d\t]", D[i])
            fi
        }
        printf("\n");
    }
}


inline print_user_unsafe()
{
    d_step {
        printf("\n\tRequired healing:");
        for ( i : 0 .. N - 1)
        {
            if
            :: unsafe[i] -> printf("\n\t    [\tTrue\t]");
            :: else      -> printf("\n\t    [\tFalse\t]");
            fi
        }
        printf("\n");
    }
}


inline print_user_messages()
{
    d_step {
        printf("\n\tMessages available:");
        for ( i : 0 .. N - 1)
        {
            if
            :: nempty( M[i] ) -> printf("\n\t    [\tTrue\t]");
            ::  empty( M[i] ) -> printf("\n\t    [\tFalse\t]");
            fi
        }
        printf("\n");
    }
}


inline print_global_state()
{
    d_step
    {
        printf("\n-=-=-=-=-=-=-=-=-=-=-=-\n-=-  GLOBAL  STATE  -=-\n-=-=-=-=-=-=-=-=-=-=-=-\n");
        print_challenges()
        print_leadership()
        print_membership()
        print_user_epochs()
        print_user_hoarding()
        print_user_messages()
        print_user_unsafe()
    }
}

/********
    *
    * Inline utilities:
    *
    *   - Select Attendee
    *   - Select Absentee
    *   - Select byte
    *
********/


/****
  * External result variable(s):
  *   - groupFull
****/
inline group_full( input )
{   d_step {

    take_attendance( input )
    groupFull = absentees == 0;

}   }


/****
  * External result variable(s):
  *   - groupDyad
****/
inline group_dyad( input )
{   d_step {

    take_attendance( input )
    groupDyad = attendees == 2;

}   }


/****
  * External result variable(s):
  *   - queueFull
****/
inline queue_full()
{   d_step {

    bool found = false
    for( i : 0 .. N - 1 )
    {
        if
        :: full( M[i] ) -> found = true
        :: else
        fi
    }

}   }


/****
  * External result variable(s):
  *   - unsafeIDs
****/
inline safety_poll()
{
    d_step {
        byte recoveriesRequired = 0;
        for ( i : 0 .. N - 1 ) {
            if
            :: unsafe[i] -> recoveriesRequired++;
            :: else
            fi
        }
        unsafeIDs = recoveriesRequired;
    }
}


/****
  * External result variable(s):
  *   - absentees
  *   - attendees
****/
inline take_attendance( t )
{   d_step {

    byte included = 0;
    byte e_i = lead[t];
    
    for ( i : 0 .. N - 1 ) {
         if
         :: G[t].membership[i] -> included++
         :: else
         fi
    }
    attendees = included;
    absentees = N - attendees;

}   }


/****
  * External result variable(s):
  *   - selectedID
****/
inline select_attendee( t )
{   atomic {

    byte bound, sample;
    byte selection = NONE;
    byte ref = lead[t]

    take_attendance( t );

    select( sample : 0 .. attendees - 1 );

/*
    printf("\nSELECTING ATTENDEE:\n");
    printf("  attendees           = %d\n", attendees);
    printf("  absentees           = %d\n", absentees);
    printf("  sample              = %d\n", sample);
*/

    for ( i : 0 .. N - 1 ) {
//        printf("  i = %d v = %d s = %d g = %d\n", i, selection, sample, selection == NONE && G[t].membership[i]);
        if
        :: selection == NONE && G[t].membership[i] && sample != 0 -> sample--
        :: selection == NONE && G[t].membership[i] && sample == 0 -> selection = i
        :: else
        fi
    }

    selectedID = selection;
//    printf("  selection           = %d\n", selectedID);

}   }


/****
  * External result variable(s):
  *   - selectedID
****/
inline select_absentee( t )
{   atomic {

    byte sample;
    byte selection = NONE;
    byte ref = lead[t]

    take_attendance( t );
    select( sample : 0 .. absentees - 1 );

/*
    printf("\nSELECTING ABSENTEE:\n");
    printf("  attendees           = %d\n", attendees);
    printf("  absentees           = %d\n", absentees);
    printf("  sample              = %d\n", sample);
*/

    for ( i : 0 .. N - 1 ) {
//        printf("  i = %d v = %d s = %d g = %d\n", i, selection, sample, selection == NONE && G[t].membership[i]);
        if
        :: selection == NONE && !G[t].membership[i] && sample != 0 -> sample--
        :: selection == NONE && !G[t].membership[i] && sample == 0 -> selection = i
        :: else
        fi
    }

    selectedID = selection;
//    printf("  selection           = %d\n", selectedID);

}   }


/****
  * External result variable(s):
  *   - senderID
****/
inline select_sender( t )
{
    byte selectedID;
    select_sender_constrained( t, NONE, false );
    senderID = selectedID;
}


/****
  * External result variable(s):
  *   - exiledID
  *
  * Similar to select_attendee
****/
inline select_exiled( t, forced )
{
    byte selectedID;
    select_sender_constrained( t, NONE, forced );
    exiledID = selectedID;
}


/****
  * External result variable(s):
  *   - banisherID
****/
inline select_banisher( t, banned )
{
    byte selectedID;
    select_sender_constrained( t, banned, false );
    banisherID = selectedID;
}


/****
  * External result variable(s):
  *   - updaterID
****/
inline select_updater( t, forced )
{
    byte selectedID;
    select_sender_constrained( t, NONE, forced );
    updaterID = selectedID;
}



/****
  * External result variable(s):
  *   - selectedID
****/
inline select_sender_constrained ( t, banned, forced )
{ atomic {

    byte candidates = 0;
    d_step {
        for ( i : 0 .. N - 1 ) {
             if
             :: G[t].membership[i] && ep[i] == t && i != banned && ((!forced && !testmailed[i]) || unsafe[i]) -> candidates++
             :: else
             fi
        }
    }

    byte sample;
    select( sample : 0 .. candidates - 1 );
    byte selection = NONE;
    d_step
    {
/**/
        printf("\nSELECTING Sender %d %d %d", t, banned, forced);
        printf("\n  candidates         = %d", candidates);
        printf("\n  sample             = %d", sample);
/**/

        for ( i : 0 .. N - 1 ) {
            printf("\n\ti = %d v = %d s = %d b = %d", i, selection, sample, G[t].membership[i] && ep[i] == t && i != banned && (!forced || unsafe[i]));
            if
            :: selection == NONE ->
                if
                ::  G[t].membership[i] && ep[i] == t && i != banned && (!forced || unsafe[i]) ->
                    if
                    :: sample == 0 -> selection = i
                    :: sample != 0 -> sample--
                    fi
                :: else
                fi
            :: else
            fi
        }
//        printf("  selection           = %d\n", senderID);

    }
    selectedID = selection;
}   }



/****
  * External result variable(s):
  *   - joinerID
****/
inline select_joiner( input )
{
    d_step
    {
        byte selectedID;
        select_absentee( input );
        joinerID = selectedID;
    }
}


/****
  * External result variable(s):
  *   - corruptedID
  *
  * Similar to select_attendee
****/
inline select_corrupted( t )
{   atomic {

    byte selection = NONE;
    safety_poll();

    byte remainingEpochs = T - t - 1

    if
    :: unsafeIDs < T - t - 1 ->
        byte candidateMembers = 0;
        d_step
        {
            for ( i : 0 .. N - 1 )
            {   // The corrupted user must not previously been instructed to hoard!
                // Violates the "Safety Predicate SAFE" described in Alwen 2020.
                if
                :: D[i] == NONE && attackerKnowledge[ep[i]].node[i] == UnknownNode -> candidateMembers++
                :: else
                fi
            }
        }
        if
        :: candidateMembers > 0 ->
            byte sample;
            select ( sample : 0 .. candidateMembers - 1 );
            for ( i : 0 .. N )
            {
                if
                :: selection == NONE ->
                    if
                    :: attackerKnowledge[ep[i]].node[i] == UnknownNode ->
                        if
                        :: sample == 0 -> selection = i
                        :: sample != 0 -> sample--
                        fi
                    :: else
                    fi
                :: else
                fi
            }
        :: else
        fi
    :: else
    fi
    corruptedID = selection

}   }


/****
  * External result variable(s):
  *   - currentEpoch
****/
inline current_epoch()
{   d_step {

    byte x = 0;
    for ( i : 0 .. T - 1 )
    {
        if
        :: lead[i] != NONE -> x++
        :: else
        fi
    }
    currentEpoch = x - 1;

}   }


/****
  * External result variable(s):
  *   - selectedEpoch
****/
inline select_epoch( input )
{   atomic {

    byte anyEpoch;
    select ( anyEpoch : 0 .. input );
    selectedEpoch = anyEpoch;

}   }


/****
  * External result variable(s):
  *   - revelationEpoch
****/
inline select_revelation( t )
{   atomic {

    byte candidateEpochs = 0
    byte challengesUsed  = 0
    d_step
    {
        for ( i : 0 .. t )
        {
            if
            ::  chall[i] -> challengesUsed++
            :: !chall[i] && attackerKnowledge[i].node[ROOT] == UnknownNode -> candidateEpochs++
            ::  else
            fi
        }
    }
    
    byte selection = NONE;
    if
    :: candidateEpochs > 0 && challengesUsed < C ->
        byte sample;
        select ( sample : 0 .. candidateEpochs - 1 );
        for ( i : 0 .. t )
        {
            if
            :: selection == NONE ->
                if
                :: !chall[i] && attackerKnowledge[i].node[ROOT] == UnknownNode ->
                    if
                    :: sample == 0 -> selection = i
                    :: sample != 0 -> sample--
                    fi
                :: else
                fi
            :: else
            fi
        }
    :: else
    fi

    revelationEpoch = selection

}   }


/****
  * External result variable(s):
  *   - hoarderID
****/
inline select_hoarder( t )
{   atomic {

    byte id  = lead[t];

    byte candidateMembers = 0
    for ( i : 0 .. N - 1 )
    {
        if
        :: D[i] == NONE && G[ep[i]].membership[i] -> candidateMembers++
        :: else -> skip
        fi
    }

    byte selection = NONE;
    if
    :: candidateMembers > 0 ->
        byte sample;
        select ( sample : 0 .. candidateMembers - 1 );
        for ( i : 0 .. N - 1 )
        {
            if
            :: sample == 0 && selection == NONE && D[i] == NONE -> selection = i
            :: sample != 0 && selection == NONE && D[i] == NONE -> sample--
            :: else -> skip
            fi
        }
    :: else -> skip
    fi

    hoarderID = selection

}   }


/****
  * External result variable(s):
  *   - messagePending
  *   - messageVacancy
****/
inline message_status()
{   d_step {

//    printf("\nCHECKING AVAILABLE MESSAGES:\n");
    byte pending = false;
    bool vacancy = true;
    for ( i : 0 .. N - 1 )
    {
        pending = pending || nempty( M[i] );
        vacancy = vacancy &&  nfull( M[i] );
    }
    messagePending = pending;
    messageVacancy = vacancy;
}   }


/****
  * External result variable(s):
  *   - recipientID
****/
inline select_recipient()
{   atomic {

//    printf("\nSELECT RECIPIENT:\n");

    byte availableRecipient = 0;
    for ( i : 0 .. N - 1 )
    {
        if
        :: nempty(M[i]) -> availableRecipient++
        ::  empty(M[i]) -> skip
        fi
    }

//    printf("\nAvailable Recipients: %d\n", availableRecipient);

    byte sample;
    select( sample : 0 .. availableRecipient - 1 )

//    printf("\nSample: %d\n", sample);

    byte selection = NONE;
    bool content;
    for ( i : 0 .. N - 1 )
    {
        content =  nempty(M[i]);
//        printf("\n  i: %d  v: %d  s: %d  q: %d\n", i, selection, sample, content);
        if
        :: selection == NONE && nempty( M[i] ) && sample == 0 -> selection = i
        :: selection == NONE && nempty( M[i] ) && sample != 0 -> sample--
        :: selection == NONE &&  empty( M[i] ) && sample == 0 -> skip
        :: selection != NONE -> skip
        fi
    }
    
//    printf("\nRecipient: %d\n", selection);
    recipientID = selection;

}   }


/****
  * External result variable(s):
  *   - forcedPlay
****/
inline forced_play( t ) {
    d_step {
        byte remainingEpochs = T - t - 1;
        safety_poll();
        assert( T - t - 1 >= unsafeIDs );
        forcedPlay = unsafeIDs > 0 && unsafeIDs == remainingEpochs;
    }
}


/****
  * External result variable(s):
  *   - commitEpochAdvance
****/
inline decide_commitment()
{
    do
    :: commitEpochAdvance = true;  break
    :: commitEpochAdvance = false; break
    od
}


/****
  *
  * The following methods:
  *   - broadcast
  *   - propogate
  *
  * Are used by the security game moves:
  *   - insert_member
  *   - remove_member
  *   - oblige_update
  *
****/


inline broadcast ( t, sender, commitment, message )
{   atomic {

    // Attacker observes network messages
    //attacker_observes_message( t, sender, commitment, message );
    
    if
    :: commitment ->
        for ( i : 0 .. N - 1 ) {
            if
            :: G[t+1].membership[i] -> M[i] ! t+1, sender;
            :: else
            fi
        };
        convey_update( sender )
    :: else
    fi

}   }


inline attacker_observes_message( t, sender, commitment, message )
{
    // Row 1, leaves
    for ( i : 0 .. N - 1 )
    {
        if
        // No knowledge from excluded group members
        :: !G[t].membership[i] ->
            attackerKnowledge[t+1].node[i] = Void
        // The sender has refreshed their secrets, hence Unknown
        :: G[t].membership[i] && (i == sender || attackerKnowledge[t].node[i] != KnownNode) ->
            attackerKnowledge[t+1].node[i] = UnknownNode
        :: G[t].membership[i] &&  i != sender && attackerKnowledge[t].node[i] == KnownNode  ->
            attackerKnowledge[t+1].node[i] = KnownNode
        fi
    }

    // Row 2, parents
    for ( i : N .. N + (N / 2) )
    {
        byte c0 = i - N
        byte c1 = i - N + 1
        if
        // If neither child exists in this epoch
        :: attackerKnowledge[t+1].node[c0] == Void && attackerKnowledge[t+1].node[c1] == Void ->
            attackerKnowledge[t+1].node[i] = Void
        // If only the left child exists in this epoch
        :: attackerKnowledge[t+1].node[c0] != Void && attackerKnowledge[t+1].node[c1] == Void ->
            if
            :: attackerKnowledge[t+1].node[c0] == KnownNode || attackerKnowledge[t+1].node[c0] == KnownRefs ->
                attackerKnowledge[t+1].node[i] =  KnownRefs
            :: else
                attackerKnowledge[t+1].node[i] = UnknownRefs
            fi
        // If only the right child exists in this epoch
        :: attackerKnowledge[t+1].node[c0] == Void && attackerKnowledge[t+1].node[c1] != Void ->
            if
            :: attackerKnowledge[t+1].node[c1] == KnownNode || attackerKnowledge[t+1].node[c1] == KnownRefs ->
                attackerKnowledge[t+1].node[i] =  KnownRefs
            :: else
                attackerKnowledge[t+1].node[i] = UnknownRefs
            fi
        // If both children exist in this epoch
        :: attackerKnowledge[t+1].node[c0] != Void && attackerKnowledge[t+1].node[c1] != Void ->
            if
            // If both children existed in the previous epoch
            :: (attackerKnowledge[t].node[c0] == KnownNode || attackerKnowledge[t].node[c0] == UnknownNode) &&
               (attackerKnowledge[t].node[c1] == KnownNode || attackerKnowledge[t].node[c1] == UnknownNode) ->
               if
               :: attackerKnowledge[t].node[c0] == KnownNode || attackerKnowledge[t].node[c0] == KnownNode ->
                   attackerKnowledge[t+1].node[i] = KnownNode
               :: else
                   attackerKnowledge[t+1].node[i] = UnknownNode
               fi
            :: else
                   attackerKnowledge[t+1].node[i] = UnknownNode            
            fi
        fi
    }

/*
    if
    :: N > 4 ->
    for ( i : N .. N + (N / 2) )
    {
        c0 = i - N
        c2 = i - N + 1
        if
        // If neither child exists in this epoch
        :: attackerKnowledge[t].node[c0] == Void && attackerKnowledge[t].node[c1] == Void ->
            attackerKnowledge[t].node[i] = Known
        // If only the left child exists in this epoch
        :: attackerKnowledge[t].node[c0] != Void && attackerKnowledge[t].node[c1] == Void ->
            attackerKnowledge[t].node[i] = Space
        // If only the right child exists in this epoch
        :: attackerKnowledge[t].node[c0] == Void && attackerKnowledge[t].node[c1] != Void ->
            attackerKnowledge[t].node[i] = Space
        // If both children exist in this epoch
        :: attackerKnowledge[t].node[c0] != Void && attackerKnowledge[t].node[c1] != Void ->
            if
            // If both children existed in the previous epoch
            :: (attackerKnowledge[e].node[c0] == Known || attackerKnowledge[e].node[c0] == Unknown) &&
               (attackerKnowledge[e].node[c1] == Known || attackerKnowledge[e].node[c1] == Unknown) && ->
               if
               :: attackerKnowledge[e].node[c0] == Known || attackerKnowledge[e].node[c0] == Known ->
                   attackerKnowledge[t].node[i] = Known
               :: else
                   attackerKnowledge[t].node[i] = Unknown
               fi
            :: else
                   attackerKnowledge[t].node[i] = Unknown                
            fi
        fi
    }
*/

}


inline propogate ( t, sender, insert, remove )
{
    d_step {
        for ( i : 0 .. N - 1 )
        {
            G[t+1].membership[i] = G[t].membership[i]
            testmailed[i] = false
        }
        if
        :: insert != NONE -> G[t+1].membership[insert] = true
        :: remove != NONE -> G[t+1].membership[remove] = false
        :: else
        fi
    }
}


/********
    *
    * Oracles available to the attacker:
    *
    *   - Corrupt
    *   - Hoard
    *   - Reveal
    *
********/


inline corrupt( memberID )
{   atomic {

    // Learn the secret material of the user in their current epoch
    byte e = ep[memberID];
    attackerKnowledge[e].node[memberID] = KnownNode;
    unsafe[memberID] = true;

    // Learn any additional secrets they have hoarded!
    byte epochSavedFrom = D[memberID];
    if
    :: epochSavedFrom < e ->
        for ( i : epochSavedFrom .. e)
        {
            attackerKnowledge[i].node[memberID] = KnownNode;
        }
    :: else
    fi

}   }


inline hoard( memberID )
{   d_step {

    D[memberID] = ep[memberID]

} }


inline reveal( t )
{
    d_step {
        chall[t] = true;
        attackerKnowledge[t].node[ROOT] -> KnownNode;
    }
}


/********
    *
    * Oracles available to the Group Members:
    *
    *   - Insert Member ( ADD )
    *   - Remove Member ( RMV )
    *   - Oblige Update ( UPD )
    *   - Convey Update ( DLV )
    *
********/


// Precondition: joiner is not in the group!
inline insert_member( t, sender, joiner, commitment )
{
    d_step {
//        printf("\nINSERT MEMBER:\n  Inviter: %d\n  joiner: %d\n  Epoch: \t%d\n", sender, joiner, t)
        assert(sender < N);
        assert(joiner < N);
    }
    if
    :: !commitment ->
        testmailed[sender] = true;
    ::  commitment ->
        d_step
        {
        propogate ( t, sender, joiner, NONE );
        }
    fi
    broadcast( t, sender, commitment, ADD );
}


// Precondition: exiledMemeber is in the group!
inline remove_member( t, sender, exiled, commitment )
{
    d_step {
        printf("\nREMOVE MEMBER:\n  Sender: \t%d\n  Exiled: \t%d\n  Epoch: \t%d\n", sender, exiled, t);
        assert(sender < N);
        assert(exiled < N);
        propogate ( t, sender, NONE, exiled);
        unsafe[exiled] = false;
    }
    broadcast( t, sender, commitment, RMV );
}


inline oblige_update( t, sender, commitment )
{
    d_step {
//        printf("\nOBLIGE UPDATE:\n  Sender: \t%d\n  Epoch: \t%d\n", sender, t);
        assert(sender < N);
        propogate ( t, sender, NONE, NONE );
        unsafe[sender] = false;
    }
    broadcast( t, sender, commitment, UPD );
}


inline convey_update( receiver )
{   atomic {

    if
    :: nempty( M[receiver] ) ->
        byte e, sender;
        printf("\nUPDATING: %d\n", receiver);

        M[receiver] ? e, sender;
        d_step {
    //        printf("\nUPDATE MESSAGE:\n  Target\t %d\n  Epoch \t %d\n  Sender\t %d\n", receiver, e, sender);
        
            if
            :: lead[e] == NONE ->
                lead[e] = sender;
                printf("SET lead[%d] <- %d\n", e, sender);
            :: else
            fi
            ep[receiver] = e;
        }
    :: empty( M[receiver] ) -> skip // There was no message
    fi
}   }


inline initialize_attacker_knowledge()
{
    d_step
    {
        for( i : 0 .. T - 1 )
        {
            for( j : N .. TREE - 1)
            {
                attackerKnowledge[i].node[j] = Void
            }
        }
    }
}


/********
    *
    * Initialization inline routines, in order of execution:
    *
    *   - Initialize
    *   - Select Group
    *   - Create Group
    *   - CGKA Security Game
    *
********/


inline CGKA_initialize()
{   atomic {
    printf("\n***********************\n* CGKA: Initialize!   *\n***********************\n");

    d_step
    {
        for( i : 0 .. N - 1 )
        {
            ep[i] = 0;
            D[i]  = NONE;
        };
    
        for( i : 0 .. T - 1 )
        {
            chall[i] = false;
            lead[i]  = NONE;
        };
    
        for ( i : 0 .. N - 1 )
        {
            testmailed[i] = false;
        }
    };

    initialize_attacker_knowledge()

}   }


inline CGKA_create_group()
{   atomic {
    printf("\n***********************\n* CGKA: Create Group! *\n***********************\n");

    byte id0, n, t;
    
    // Set the "lead" byte to be the first member in the group.
    d_step
    {
        id0 = 0;
        t   = 0;
        lead[t] = id0;
    }

    // Number of members to add
    select ( n : 2 .. N );
    for( i : 0 .. N - 1 )
    {
        G[t].membership[i] = i < n;
    };

    print_membership();
    
    broadcast ( t, id0, true, UPD );
    
    for( i : 0 .. N - 1 )
    {
        if
        :: G[t].membership[i] -> convey_update( i )
        :: else
        fi
    };

}   }


inline CGKA_security_game()
{
    printf("\n***********************\n* CGKA: Begin Play!   *\n***********************\n");
    bool endGame = false;
    endHere:
    do
    :: !endGame -> atomic
        {
            byte currentEpoch, revelationEpoch;
            bool forcedPlay, groupDyad, groupFull, messagePending, messageVacancy;
            byte corruptedID, exiledID, banisherID, hoarderID, updaterID;

            atomic
            {
//                printf("\nPRECOMPUTE GUARDS:\n");
    
                current_epoch();
                message_status();
                forced_play(       currentEpoch );
                group_dyad(        currentEpoch );
                group_full(        currentEpoch );
                select_revelation( currentEpoch );
                select_corrupted(  currentEpoch );
                select_hoarder(    currentEpoch );
                select_updater(    currentEpoch, forcedPlay );
                select_exiled(     currentEpoch, forcedPlay );
                select_banisher(   currentEpoch, exiledID   );
            };

            print_global_state()

/*
            d_step
            {
                printf("  currentEpoch    = %d\n", currentEpoch);
                printf("  messagePending  = %d\n", messagePending);
                printf("  messageVacancy  = %d\n", messageVacancy);
                printf("  forcedPlay,     = %d\n", forcedPlay);
                printf("  groupDyad       = %d\n", groupDyad);
                printf("  groupFull       = %d\n", groupFull);
                printf("  absentees       = %d\n", absentees);
                printf("  attendees       = %d\n", attendees);
                printf("  revelationEpoch = %d\n", revelationEpoch);
                printf("  corruptedID     = %d\n", corruptedID);
                printf("  hoarderId       = %d\n", hoarderID);
            }
*/
/*
            d_step
            {
                printf("\nPRINT GLOBAL STATE:\n");
    
                printf("\n  lead:\n    [ ");
                for ( i : 0 .. T - 1)
                {
                    if
                    :: i == 0 -> printf(  "%d ", lead[i])
                    :: i != 0 -> printf("| %d ", lead[i])
                    fi
                }
                printf("]\n");
    
                printf("\n  chall:\n    [ ");
                for ( i : 0 .. T - 1)
                {
                    if
                    :: i == 0 -> printf(  "%d ", chall[i])
                    :: i != 0 -> printf("| %d ", chall[i])
                    fi
                }
                printf("]\n");
    
                printf("\n  ep:\n    [ ");
                for ( i : 0 .. T - 1)
                {
                    if
                    :: i == 0 -> printf(  "%d ", ep[i])
                    :: i != 0 -> printf("| %d ", ep[i])
                    fi
                }
                printf("]\n");
    
                printf("\n  M:\n    [ ");
                for ( i : 0 .. N - 1)
                {
                    byte v = len(M[i]);
                    if
                    :: i == 0 -> printf(  "%d ", v)
                    :: i != 0 -> printf("| %d ", v)
                    fi
                }
                printf("]\n");
            };
*/

            do
            /****************************************************
             *
             *  Corrupt
            **/
            :: corruptedID != NONE && !forcedPlay -> atomic
                {
                    printf("\n> > >\n> CGKA: Game Move = corrupt %d\n> > >\n", corruptedID);
                    corrupt( corruptedID )
                }; break
            /****************************************************
             *
             *  Hoard
            **/
            :: hoarderID != NONE -> atomic
                {
                    printf("\n> > >\n> CGKA: Game Move = hoard %d\n> > >\n", hoarderID);
                    hoard( hoarderID )
                }; break
            /****************************************************
             *
             *  Reveal
            **/
            :: revelationEpoch != NONE -> atomic
                {
                    printf("\n> > >\n> CGKA: Game Move = reveal %d\n> > >\n", revelationEpoch);
                    reveal( revelationEpoch )
                }; break
            /****************************************************
             *
             *  Convey
            **/
            :: messagePending -> atomic
                {   // Selection
                    byte recipientID;
                    select_recipient();
                    // Execution
                    printf("\n> > >\n> CGKA: Game Move = convey_update %d\n> > >\n", recipientID);
                    convey_update ( recipientID );
                }; break
            /****************************************************
             *
             *  Update
            **/
            :: messageVacancy && updaterID != NONE -> atomic
                {
                    bool commitEpochAdvance;
                    decide_commitment();
                    printf("\n> > >\n> CGKA: Game Move = oblige_update %d (committed? %d)\n> > >\n", updaterID, commitEpochAdvance);
                    oblige_update( currentEpoch, updaterID, commitEpochAdvance );
                    convey_update( updaterID );
                }; break
            /****************************************************
             *
             *  Remove
            **/
            :: !groupDyad && messageVacancy && exiledID != NONE && banisherID != NONE -> atomic
                {
                    bool commitEpochAdvance;
                    decide_commitment();
                    printf("\n> > >\n> CGKA: Game Move = remove_member %d %d (committed? %d)\n> > >\n", banisherID, exiledID, commitEpochAdvance);
                    remove_member( currentEpoch, banisherID, exiledID, commitEpochAdvance );
                    convey_update( banisherID );
                }; break
            /****************************************************
             *
             *  Insert
            **/
            :: !groupFull && !forcedPlay && messageVacancy -> atomic
                {   // Selection
                    byte joinerID, senderID;
                    select_sender( currentEpoch );
                    select_joiner( currentEpoch );
                    bool commitEpochAdvance;
                    decide_commitment();
                    // Execution
                    printf("\n> > >\n> CGKA: Game Move = insert_member %d %d (committed? %d)\n> > >\n", senderID, joinerID, commitEpochAdvance);
                    insert_member( currentEpoch, senderID, joinerID, commitEpochAdvance );
                    convey_update( senderID );
                }; break
            od;

           /**
             *  Post game move update.
            **/
            d_step
            {
                // After the operation is complete, check to see if the an endgame condition has been reached.
                current_epoch();
                safety_poll();
                endGame = lead[T-1] != NONE && unsafeIDs == 0;
                printf ("\n< < <\n< Moves:   %d\n< Unsafe:  %d\n< Endgame: %d\n< < < \n", T - currentEpoch - 1, unsafeIDs, endGame);
            };
        }
    :: else -> break
    od   
}


inline CGKA_safety_check()
{
    printf("\n***********************\n* CGKA: Safety Check! *\n***********************\n");

    printf("\n? ? ? ? ? ? ? ? ? ? ? ?\n? Attacker  Knowledge ?\n? ? ? ? ? ? ? ? ? ? ? ?\n");
    
    for( t : 0 .. T - 1 )
    {
        printf("\n>>> %d vvv", t);
        for( i : 0 .. TREE - 1 )
        {
            if
            :: attackerKnowledge[t].node[i] == UnknownRefs -> printf("\n\t%d [ x ]", i)
            :: attackerKnowledge[t].node[i] == UnknownNode -> printf("\n\t%d [ X ]", i)
            :: attackerKnowledge[t].node[i] ==   KnownRefs -> printf("\n\t%d [ o ]", i)
            :: attackerKnowledge[t].node[i] ==   KnownNode -> printf("\n\t%d [ O ]", i)
            :: else                                        -> printf("\n\t%d [ ? ]", i)
            fi
        }
    };
    printf("\n");
}


inline new_loop_outline()
{
    for ( t : 0 .. T - 2)
    {
        bool testmailed[N];
        d_step
        {
            for ( i : 0 .. N - 1 )
            {
                testmailed[i] = false;
            }
        };

        bool commitmentRequired = false;
        
        do
        :: play_move_with_commitment( t ); break
        :: !commitmentRequired -> atomic
            {
               play_noncommittal_move( t );
               check_commitment_required( t );
            };
        od;

        d_step
        {
            // After the operation is complete, check to see if the an endgame condition has been reached.
            safety_poll();
            printf ("\n< < <\n< Moves:   %d\n< Unsafe:  %d\n< < < \n", T - t - 1, unsafeIDs);
        }
    }
/*
    bool endGame = false;
    do
    :: endGame -> break
    :: atomic
        {
           play_noncommittal_move( t );
           d_step
           {
               // After the operation is complete, check to see if the an endgame condition has been reached.
               safety_poll();
               endGame = lead[T-1] != NONE && unsafeIDs == 0;
               printf ("\n< < <\n< Moves:   %d\n< Unsafe:  %d\n< Endgame: %d\n< < < \n", T - t - 1, unsafeIDs, endGame);
           }
        };
    od;
*/
}


inline check_commitment_required( t )
{
    bool canTestMail = false;
    d_step
    {
        for ( i : 0 .. N - 1 )
        {
            canTestMail = canTestMail || !testmailed[i];
        }
    };

    byte challengesUsed = 0;
    d_step
    {
        for ( i : 0 .. t )
        {
            if
            :: chall[i] -> challengesUsed++;
            :: else
            fi
        }
    };
    bool canRevealEpoch = !chall[t] && challengesUsed < C;

    bool canHoardMember = false;
    d_step
    {
        for ( i : 0 .. N - 1 )
        {
            canHoardMember = canHoardMember || D[i] != NONE
        }
    }

    bool canCorruptMember = false;
    d_step
    {
        byte corruptedID = NONE;
        select_corrupted( t );
        canCorruptMember = corruptedID != NONE;
    }

    commitmentRequired = !canTestMail && !canRevealEpoch && !canHoardMember && !canCorruptMember
}


inline play_noncommittal_move( t )
{
    bool forcedPlay, groupDyad, groupFull, messagePending, messageVacancy;
    byte corruptedID, exiledID, banisherID, hoarderID, updaterID, revelationEpoch;

    assign_move_variables( t, false );
    printf("\nNON-Committal!");

    do
    // Non-committing moves
    :: play_move_maybe_commitment( t, false ); break

    //  Corrupt
    :: corruptedID != NONE && !forcedPlay -> atomic
        {   printf("\n> > >\n> CGKA: Game Move = corrupt %d\n> > >\n", corruptedID);
            corrupt( corruptedID )
        }; break

    //  Hoard
    :: hoarderID != NONE -> atomic
        {   printf("\n> > >\n> CGKA: Game Move = hoard %d\n> > >\n", hoarderID);
            hoard( hoarderID )
        }; break

    //  Reveal
    :: revelationEpoch != NONE -> atomic
        {   printf("\n> > >\n> CGKA: Game Move = reveal %d\n> > >\n", revelationEpoch);
            reveal( revelationEpoch )
        }; break

    //  Convey
    :: messagePending -> atomic
        {
            byte recipientID;
            select_recipient();
            printf("\n> > >\n> CGKA: Game Move = convey_update %d\n> > >\n", recipientID);
            convey_update ( recipientID );
        }; break
    od
}


inline play_move_with_commitment( t )
{
    bool forcedPlay, groupDyad, groupFull, messagePending, messageVacancy;
    byte corruptedID, exiledID, banisherID, hoarderID, updaterID, revelationEpoch;
    
    assign_move_variables( t, true );
    printf("\nCOMMITTING!");
    play_move_maybe_commitment( t, true )
}


inline assign_move_variables( t, commitment )
{
    atomic
    {
        message_status();
        forced_play(       t );
        group_dyad(        t );
        group_full(        t );
        select_revelation( t );
        select_corrupted(  t );
        select_hoarder(    t );
        select_updater(    t, forcedPlay && commitment );
        select_exiled(     t, forcedPlay && commitment );
        select_banisher(   t, exiledID   );
    };
    printf("\nEpoch (t) = %d", t);
    print_global_state()
}


inline play_move_maybe_commitment( t, commitment )
{
    do
    // Update
    :: messageVacancy && updaterID != NONE -> atomic
        {   printf("\n> > >\n> CGKA: Game Move = oblige_update %d (committed? %d)\n> > >\n", updaterID, commitment);
            oblige_update( t, updaterID, commitment );
        }; break

    // Remove
    :: !groupDyad && messageVacancy && exiledID != NONE && banisherID != NONE -> atomic
        {  printf("\n> > >\n> CGKA: Game Move = remove_member %d %d (committed? %d)\n> > >\n", banisherID, exiledID, commitment);
            remove_member( t, banisherID, exiledID, commitment );
        }; break

    // Insert
    :: !groupFull && !forcedPlay && messageVacancy -> atomic
        {
            byte joinerID, senderID;
            select_sender( t );
            select_joiner( t );
            printf("\n> > >\n> CGKA: Game Move = insert_member %d %d (committed? %d)\n> > >\n", senderID, joinerID, commitment);
            insert_member( t, senderID, joinerID, commitment );
        }; break
    od
}


init
{
    CGKA_initialize();
    CGKA_create_group();
//    new_loop_outline();
    CGKA_security_game();
    CGKA_safety_check();
}


ltl attendees_more_than_one { [](attendees > 1) }
//ltl attendees_absentees_sum { [](attendees + absentees == N) }
