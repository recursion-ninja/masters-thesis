/********
    *
    * Advantage:
    *
    * In the following, a (`t`, `c`, `n`)-attacker is an attacker `A` that runs in time at most `t`,
    * makes at most `c` challenge queries, and never produces a group with more than `n` members.
    * The attacker wins the CGKA security game if they correctly guesses the random bit `b` in
    * the end and the safety predicate `P` evaluates to true on the queries made by the attacker.
    *
    * Security game's parameters' valid ranges:
    *   - T <- [3, 254]
    *   - C <- [1,   T]
    *   - N <- [3, 254] 
    *
    * Hence we define our (`t`, `c` `n`) constants below:
    *
********/
#define C 3
#define N 4
#define T 3


/****
  *
  * Number of bits required to represent:
  *   - N + 1
  *   - T
  *   - The maximum of T and (N + N - 1) 
  *
****/
#define N_BITS 3
#define T_BITS 2
#define X_BITS 5


/****
  *
  * Lookup table for TREE value derived from N:
  *   - [ 1,   1] ->   1
  *   - [ 2,   2] ->   3
  *   - [ 3,   4] ->   7
  *   - [ 5,   8] ->  15
  *   - [ 9,  16] ->  31
  *   - [17,  32] ->  63
  *   - [33,  64] -> 127
  *   - [65, 128] -> 255
  *
****/
#define TREE 7


/****
  *
  * Constants, respectively for the following:
  *   - Sentinal value for missing data. Value should equal (2 ^ N_BITS - 1)
  *   - Index of the root node in the heap
  *   - Offset to index the first node of the heap
  *   - Final epoch of the security game with implicit "challenge"
  *   - Number of revelations the attacker may make during the game.
  *     Note that revelation moves require using a challenge and,
  *     because the last challenge must be saved to end the game,
  *     a revelation move cannot use the last available challenge.
  *
****/
//#define NONE 7
#define NONE ( (1 << N_BITS) - 1 )
#define ROOT 0
#define NODE (TREE / 2)
#define LAST_EPOCH (T - 1)
#define MAX_REVEAL (C - 1)


/****
  *
  * Global state of the CGKA security game.
  *
****/

local unsigned epoch     : T_BITS; // The current epoch
local unsigned unsafeIDs : N_BITS; // Number of unsafeIDs

local bool           challenge[T]; // Has the attacker challenged in an epoch?
local bool          membership[N]; // Group membership of current epoch
local bool              unsafe[N]; // Members which require a change to update
local byte            hoarding[N]; // Epoch from which the user saves secrets

local bool concludedCGKA = false; // Does the CGKA end
// Does a trivial attack exist?
// i.e, does the SAFE predicate hold?
local bool triviality   = false; 


/****
  *
  * Global state of the attacker.
  *
****/

//        Void - This node does not exist  and there exists 0 leaves in the subtree
// UnknownRefs - This node does not exist  and there exists 1 leaf   in the subtree
//   KnownRefs - This node does not exist  and there exists 1 leaf   in the subtree
// UnknownNode - This node          exists and there exists 1 or more leaves in the subtree
//   KnownNode - This node          exists and there exists 1 or more leaves in the subtree
mtype:Secret = { Void, UnknownRefs, KnownRefs, UnknownNode, KnownNode };

// Required for a multidimensional array
typedef TreeKeys   { mtype:Secret node[ TREE ]  };
TreeKeys attackerKnowledge[T];

/****
  *
  * Left-balanced Binary Tree:
  *
  *            +-----(14)-----+
  *           /                \
  *         (12)              (13)
  *        /    \            /    \
  *     (8)     (9)       (10)     (11)
  *    /   \   /   \     /    \   /    \
  *   0     1 2     3   4      5 6      7 
  *
  * Binary Heap Layout:
  *
  * Index:   0   1   2   3   4   5   6   7   8   9  10  11  12  13  14
  * Array: [  ][  ][  ][  ][  ][  ][  ][  ][  ][  ][  ][  ][  ][  ][  ]
  * Node:   14  12  13   8   9  10  11   0   1   2   3   4   5   6   7
  *
****/

local bool attackerKnowsRootKey = false;


/****
  *
  * Global (local) variable for inline functions.
  *
****/

// Indexing variables
local unsigned i : X_BITS = 0;
local unsigned j : X_BITS = 0;
local unsigned k : X_BITS = 0;
//local unsigned t : T_BITS = 0;

// Group Composition
local unsigned absentees : N_BITS = 0;
local unsigned attendees : N_BITS = N;
local unsigned groupMost : N_BITS = 0; // The maximum member ID during any past/present epoch.
local bool groupDyad = false;
local bool groupFull = false;

// Protocol State
local bool forcedPlay = false;
local bool revealRoot = C > 1;

/********
    *
    * Global state priniting utilities:
    *
    *   - print_attacker_knowledge
    *   - print_challenges
    *   - print_group_composition
    *   - print_membership
    *   - print_protocol_state
    *   - print_user_hoarding
    *   - print_user_unsafe
    *
********/


inline print_attacker_knowledge()
{
    d_step
    {
        unsigned t : T_BITS;
        printf("\n\tAttacker Knowledge:");
        for( t : 0 .. T - 1 )
        {
            printf("\n>>> %d vvv", t);
            for( j : 0 .. TREE - 1 )
            {
                if
                :: attackerKnowledge[t].node[j] == UnknownRefs -> printf("\n\t%d [ x ]", j)
                :: attackerKnowledge[t].node[j] == UnknownNode -> printf("\n\t%d [ X ]", j)
                :: attackerKnowledge[t].node[j] ==   KnownRefs -> printf("\n\t%d [ o ]", j)
                :: attackerKnowledge[t].node[j] ==   KnownNode -> printf("\n\t%d [ O ]", j)
                :: attackerKnowledge[t].node[j] ==        Void -> printf("\n\t%d [   ]", j)
                :: else                                        -> printf("\n\t%d [ ? ]", j)
                fi
            }
        };
        printf("\n");
    }
}


inline print_challenges()
{
    d_step {
        printf("\n\tChallenges:");
        for ( i : 0 .. T - 1)
        {
            if
            :: challenge[i] -> printf("\n\t    [\tTrue\t]");
            :: else     -> printf("\n\t    [\tFalse\t]");
            fi
        }
        printf("\n");
    }
}


inline print_group_composition()
{
    d_step
    {
        printf("\n\tattendees \t%d", attendees );
        printf("\n\tabsentees \t%d", absentees );
        printf("\n\tgroupDyad \t%d", groupDyad );
        printf("\n\tgroupFull \t%d", groupFull );
        printf("\n\tgroupMost \t%d", groupMost );
    }
}


inline print_membership()
{
    d_step {
        printf("\n\tMembership:");
        for( i : 0 .. N - 1 )
        {
            if
            :: membership[i] -> printf("\n\t    [\tTrue\t]");
            :: else          -> printf("\n\t    [\tFalse\t]");
            fi
        };
        printf("\n");
    }
}


inline print_protocol_state()
{
    d_step
    {
        printf("\n\trevealRoot \t%d", revealRoot );
        printf("\n\tforcedPlay \t%d", forcedPlay );
        printf("\n\tunsafeIDs  \t%d", unsafeIDs  );
    }
}


inline print_user_hoarding()
{
    d_step {
        printf("\n\tHoarding since:");
        for ( i : 0 .. N - 1)
        {
            if
            :: hoarding[i] == NONE -> printf("\n\t    [\tNONE\t]")
            :: else                -> printf("\n\t    [\t%d\t]", hoarding[i])
            fi
        }
        printf("\n");
    }
}


inline print_user_unsafe()
{
    d_step {
        printf("\n\tRequired healing:");
        for ( i : 0 .. N - 1)
        {
            if
            :: unsafe[i] -> printf("\n\t    [\tTrue\t]");
            :: else      -> printf("\n\t    [\tFalse\t]");
            fi
        }
        printf("\n");
    }
}


inline print_global_state()
{
    d_step
    {
        printf("\n-=-=-=-=-=-=-=-=-=-=-=-\n-=-  GLOBAL  STATE  -=-\n-=-=-=-=-=-=-=-=-=-=-=-\n");
        print_challenges();
        print_membership();
        print_user_hoarding();
        print_user_unsafe();
        print_group_composition();
        print_protocol_state();
        print_attacker_knowledge()
    }
}


/********
    *
    * Inline utilities for after move updates:
    *   - after_move_refresh
    *   - take_attendance
    *
********/


/****
  * External result variable(s):
  *   - commitmentRequired
  *   - forcedPlay
  *   - revealRoot
  *   - triviality
  *   - unsafeIDs
****/
inline after_move_refresh( t )
{
    d_step {
        // Refresh "unsafeIDs"
        unsigned remainingEpochs    : T_BITS = LAST_EPOCH - t;
        unsigned recoveriesRequired : N_BITS = 0;
        for ( i : 0 .. N - 1 ) {
            if
            :: unsafe[i] -> recoveriesRequired++;
            :: else
            fi
        }
        unsafeIDs  = recoveriesRequired;

        // Refresh "triviality"
        triviality = unsafeIDs > 0; 

        // Refresh "forcedPlay"
        forcedPlay = unsafeIDs > 0 && unsafeIDs == remainingEpochs;
        printf("\n\tremainingEpochs\t%d\n\tunsafeIDs\t%d\n", remainingEpochs, unsafeIDs);

        // Refresh "revealRoot"
        revealRoot = !challenge[t] && (t != LAST_EPOCH) && attackerKnowledge[t].node[ROOT] == UnknownNode;
        if
        :: !revealRoot -> skip
        :: else ->
            unsigned challengesUsed : T_BITS = 0;
            d_step
            {
                for ( i : 0 .. t )
                {
                    if
                    :: challenge[i] -> challengesUsed++;
                    :: else
                    fi
                }
            };
            revealRoot = challengesUsed < MAX_REVEAL;
        fi

        // Refresh "commitmentRequired"
        bool canHoardMember = false;
        d_step
        {
            unsigned candidateHoarders : N_BITS;
            candidates_for_hoarding();
            canHoardMember = candidateHoarders > 0;
    //        printf("\n canHoardMember = %d", canHoardMember);
        }
    
        bool canCorruptMember = false;
        d_step
        {
            unsigned candidateCorruptibles : N_BITS;
            candidates_for_corruption( t );
            canCorruptMember = candidateCorruptibles > 0;
    //        printf("\n canCorruptMember = %d", canCorruptMember);
        }
    
        commitmentRequired = !revealRoot && !canHoardMember && !canCorruptMember
    }
}


/****
  * External result variable(s):
  *   - absentees
  *   - attendees
  *   - groupDyad
  *   - groupFull
  *   - groupMost
****/
inline take_attendance()
{
    unsigned largestID : N_BITS;
    d_step {
        unsigned included : N_BITS = 0;
        for ( i : 0 .. N - 1 ) {
             if
             :: membership[i] -> included++; largestID = i
             :: else
             fi
        }
        attendees = included;
        absentees = N - attendees;
        groupDyad = attendees == 2;
        groupFull = absentees == 0;
    }

    if
    :: largestID + 1 > groupMost -> groupMost = largestID + 1;
    :: else
    fi
    d_step
    {
        printf("\n\tattendees \t%d", attendees );
        printf("\n\tabsentees \t%d", absentees );
        printf("\n\tgroupDyad \t%d", groupDyad );
        printf("\n\tgroupFull \t%d", groupFull );
        printf("\n\tgroupMost \t%d", groupMost );
    }
}


/********
    *
    * User ID selection inlines:
    *   - select_corrupted
    *   - select_banisher
    *   - select_exiled
    *   - select_hoarder
    *   - select_joiner
    *   - select_sender
    *   - select_updater
    *
********/


/****
  * External result variable(s):
  *   - corruptedID
****/
inline select_corrupted()
{   atomic {

    unsigned candidateCorruptibles : N_BITS;
    candidates_for_corruption( epoch );
    if
    :: candidateCorruptibles == 0 -> corruptedID = NONE
    :: else ->
        unsigned selection : N_BITS = NONE;
        unsigned sample : N_BITS;
        select ( sample : 0 .. candidateCorruptibles - 1 );
        for ( i : 0 .. N )
        {
            d_step
            {
                if
                :: selection == NONE ->
                    bool candidateCorruption;
                    candidate_corruption( i );
                    if
                    :: candidateCorruption ->
                        if
                        :: sample == 0 -> selection = i
                        :: sample != 0 -> sample--
                        fi
                    :: else
                    fi
                :: else
                fi
            }
        }
        corruptedID = selection
    fi
}   }


/****
  * External result variable(s):
  *   - candidateCorruptibles
****/
inline candidates_for_corruption( t )
{
    unsigned remaining : T_BITS = LAST_EPOCH - t;

    if
    :: unsafeIDs >= remaining -> candidateCorruptibles = 0;
    :: else ->
        unsigned candidates : N_BITS = 0;
        for ( i : 0 .. N - 1 )
        {
            bool candidateCorruption;
            candidate_corruption( i );
            if
            :: candidateCorruption -> candidates++
            :: else
            fi
        }
        candidateCorruptibles = candidates
    fi
}


/****
  * External result variable(s):
  *   - candidateCorruption
****/
inline candidate_corruption( id )
{
    // The corrupted user must not previously been instructed to hoard!
    // Violates the "Safety Predicate SAFE" described in Alwen 2020.
    candidateCorruption = hoarding[id] == NONE && membership[id] && attackerKnowledge[epoch].node[NODE+id] == UnknownNode
}


/****
  * External result variable(s):
  *   - banisherID
****/
inline select_banisher( banned )
{
    unsigned selectedID : N_BITS;
    select_sender_constrained( banned, false );
    banisherID = selectedID;
}


/****
  * External result variable(s):
  *   - exiledID
****/
inline select_exiled( forced )
{
    unsigned selectedID : N_BITS;
    select_sender_constrained( NONE, forced );
    exiledID = selectedID;
}


/****
  * External result variable(s):
  *   - hoarderID
****/
inline select_hoarder()
{   atomic {

    unsigned candidateHoarders : N_BITS;
    candidates_for_hoarding();

    if
    :: candidateHoarders == 0 -> hoarderID = NONE
    :: else ->
        unsigned selection : N_BITS = NONE;
        unsigned sample : N_BITS;
        select ( sample : 0 .. candidateHoarders - 1 );
        for ( i : 0 .. N - 1 )
        {
            d_step
            {
                if
                :: selection != NONE -> skip
                :: else ->
                    bool candidateHoarder
                    candidate_hoarder( i );
                    if
                    :: !(candidateHoarder) -> skip
                    :: else ->
                        if
                        :: sample == 0 -> selection = i
                        :: sample != 0 -> sample--
                        fi
                    fi
                fi
            }
        }
        hoarderID = selection
    fi
}   }


/****
  * External result variable(s):
  *   - candidateHoarders
****/
inline candidates_for_hoarding()
{
    unsigned candidates : N_BITS = 0;
    for ( i : 0 .. N - 1 )
    {
        bool candidateHoarder
        candidate_hoarder( i );
        if
        :: candidateHoarder -> candidates++
        :: else
        fi
    }
    candidateHoarders = candidates
}


/****
  * External result variable(s):
  *   - candidateHoarder
****/
inline candidate_hoarder( i )
{
    candidateHoarder = hoarding[i] == NONE && membership[i]
}


/****
  * External result variable(s):
  *   - joinerID
****/
inline select_joiner()
{   atomic {

    unsigned candidateJoiners : N_BITS;
    candidates_for_joiner();

    unsigned selection : N_BITS = NONE;
    unsigned sample : N_BITS;
    select(  sample : 0 .. candidateJoiners - 1 );

    for ( i : 0 .. N - 1 ) {
        d_step
        {
            if
            :: selection != NONE || membership[i] -> skip
            :: else ->
                if
                :: sample != 0 -> sample--
                :: sample == 0 -> selection = i
                fi
            fi
        }
    }
    
    joinerID = selection;
}   }


/****
  * External result variable(s):
  *   - candidateJoiners
****/
inline candidates_for_joiner()
{
    unsigned candidates : N_BITS = 0;
    for ( i : 0 .. groupMost - 1 )
    {
        if
        :: !(membership[i]) -> candidates++
        :: else
        fi
    }

    printf("Candidate Joiners (1): %d", candidates);

    // Consider adding the next user who has never been a member before...
    // but make the consideration *if and only if* such a user exists.
    if
    :: groupMost == N -> skip
    :: else -> candidates++
    fi
    
    printf("Candidate Joiners (2): %d", candidates);

    candidateJoiners = candidates
}


/****
  * External result variable(s):
  *   - senderID
****/
inline select_sender()
{
    unsigned selectedID : N_BITS;
    select_sender_constrained( NONE, false );
    senderID = selectedID;
}


/****
  * External result variable(s):
  *   - updaterID
****/
inline select_updater( forced )
{
    unsigned selectedID : N_BITS;
    select_sender_constrained( NONE, forced );
    updaterID = selectedID;
}


/****
  * External result variable(s):
  *   - selectedID
****/
inline select_sender_constrained ( banned, forced )
{   atomic {
    unsigned candidates : N_BITS = 0;

    for ( i : 0 .. N - 1 )
    {
        bool senderCandidate;
        sender_candidate( banned, forced, i );
        if
        :: senderCandidate -> candidates++
        :: else
        fi
    }

    if
    :: candidates == 0 -> selectedID = NONE
    :: else ->
        unsigned selection : N_BITS = NONE;
        unsigned sample : N_BITS;
        select(  sample : 0 .. candidates - 1 );
        for ( i : 0 .. N - 1 ) {
            d_step
            {
                if
                :: selection == NONE ->
                    bool senderCandidate;
                    sender_candidate( banned, forced, i );
                    if
                    :: senderCandidate ->
                        if
                        :: sample == 0 -> selection = i
                        :: sample != 0 -> sample--
                        fi
                    :: else
                    fi
                :: else
                fi
            }
        }
        selectedID = selection;
    fi
}   }


/****
  * External result variable(s):
  *   - senderCandidate
****/
inline sender_candidate( banned, forced, id )
{
//    bool querryOkay = commitment || !(epochQuerried[id]);
//    bool forcesSafe = (!(forced) && querryOkay) || unsafe[id];
    bool forcesSafe = !(forced)  || unsafe[id];
    bool isAnOption = membership[id] && (id != banned);
    senderCandidate = isAnOption && forcesSafe
}


/****
  *
  * The following methods:
  *   - broadcast
  *   - propogate
  *
  * Are used by the security game moves:
  *   - insert_member
  *   - remove_member
  *   - oblige_update
  *
****/


inline broadcast ( t, sender, subject )
{
    attacker_observes_message( t, sender, subject );
}


inline propogate ( sender, insert, remove )
{
    d_step {
        if
        :: insert != NONE -> membership[insert] = true
        :: remove != NONE -> membership[remove] = false
        :: else
        fi

        take_attendance();
    }
}


/****
  *
  * The following methods:
  *   - attacker_initializes_knowledge
  *   - attacker_insight_check
  *   - attacker_observes_message
  *
  * Are used by the security game moves:
  *   - broadcast
  *   - CGKA_initialize
  *   - corrupt
  *
****/


inline attacker_initializes_knowledge()
{
    d_step
    {
        for( i : 0 .. T - 1 )
        {
            for( j : 0 .. TREE - 1 )
            {
                attackerKnowledge[i].node[j] = Void
            }
        }
        attackerKnowsRootKey = false;
    }
}


inline attacker_observes_message( t, sender, subject )
{
    atomic
    {
        // If the attacker has know knowledge of the epoch,
        // meaning that all cell values are "Void,"
        // then it is the first time we have entered the epoch
        // and the cell values should be initialized as either:
        //   * UnknownNode
        //   * UnknownRefs
        //   *        Void
        bool noEpochKnowledge;
        attacker_has_no_epoch_knowledge( t );
        if
        :: noEpochKnowledge -> attacker_init_epoch_knowledge( t );
        :: else
        fi
        
        //       referenceEpoch = (t == 0) ? t : t - 1;
        unsigned referenceEpoch : T_BITS = t ;
        if
        :: t != 0 -> referenceEpoch--;
        :: else
        fi

        attacker_copy_epoch_knowledge( referenceEpoch );
        attacker_wipe_sender_knowledge( sender, t );
        attacker_updates_knowledge ( t );
        attacker_insight_check( t );
    }
}

inline attacker_insight_check( t )
{
    bool anyRootKnown = false;
    d_step
    {
        for( i : 0 .. t )
        {
            anyRootKnown = anyRootKnown || attackerKnowledge[i].node[ROOT]
        }
    }
    attackerKnowsRootKey = anyRootKnown;
}



inline attacker_has_no_epoch_knowledge ( e )
{
    d_step
    {
        bool allVoid = true;
        for ( i : 0 .. TREE - 1 )
        {
            allVoid = allVoid && (attackerKnowledge[e].node[i] == Void)
        }
        noEpochKnowledge = allVoid;
    }
}


inline attacker_init_epoch_knowledge( e )
{   atomic {

    bool leaves = true;
    unsigned offset : X_BITS = NODE;
    unsigned width  : X_BITS = TREE + 1;
    do
    :: width == 0 -> break
    :: width != 0 -> d_step
        {
            width = width / 2;
            for ( i : 0 .. width - 1 )
            {
                j = offset + i;
                // Leaf node case(s)
                if
                :: leaves ->
                    if
                    // No knowledge from excluded group members
                    :: !(membership[i]) -> attackerKnowledge[e].node[j] = Void
                    :: else             -> attackerKnowledge[e].node[j] = UnknownNode
                    fi
                // Internal node case(s)
                :: else ->
                    unsigned childL : X_BITS = j * 2 + 1;
                    unsigned childR : X_BITS = j * 2 + 2;
                    // Check current epoch for existance of subtrees
                    bool existanceOfSubtree;
                    existance_of_subtree( e, childL);
                    bool voidL = !existanceOfSubtree;
                    existance_of_subtree( e, childR);
                    bool voidR = !existanceOfSubtree;
                    if
                    ::  voidL &&  voidR -> attackerKnowledge[e].node[j] =        Void
                    :: !voidL &&  voidR -> attackerKnowledge[e].node[j] = UnknownRefs
                    ::  voidL && !voidR -> attackerKnowledge[e].node[j] = UnknownRefs
                    :: !voidL && !voidR -> attackerKnowledge[e].node[j] = UnknownNode
                    fi
                fi
            };
            offset = offset / 2;
            leaves = false;
        }
    od
}   }


inline attacker_copy_epoch_knowledge( e )
{   atomic {

    unsigned offset : X_BITS = NODE;
    unsigned width  : X_BITS = TREE + 1;
    do
    :: width == 0 -> break
    :: width != 0 -> d_step
        {
            width = width / 2;
            for ( i : 0 .. width - 1 )
            {
                j = offset + i;
                bool knowledgeOfSubtree;
                knowledge_of_subtree( e, j);
                if
                ::  attackerKnowledge[e+1].node[j] == UnknownNode && knowledgeOfSubtree ->
                    attackerKnowledge[e+1].node[j] = KnownNode
                ::  attackerKnowledge[e+1].node[j] == UnknownRefs && knowledgeOfSubtree ->
                    attackerKnowledge[e+1].node[j] = KnownRefs
                :: else
                fi
            };
            offset = offset / 2;
        }
    od
}   }


inline attacker_wipe_sender_knowledge( sender, e )
{   atomic {

    unsigned offset : X_BITS = NODE;
    unsigned width  : X_BITS = TREE + 1;
    do
    :: width == 0 -> break
    :: width != 0 -> d_step
        {
            width = width / 2;
            j = offset + sender;
            if
            ::  attackerKnowledge[e].node[j] == UnknownNode ||
                attackerKnowledge[e].node[j] ==   KnownNode ->
                attackerKnowledge[e].node[j] =  UnknownNode
            ::  attackerKnowledge[e].node[j] == UnknownRefs ||
                attackerKnowledge[e].node[j] ==   KnownRefs ->
                attackerKnowledge[e].node[j] =  UnknownRefs
            :: else
            fi
            offset = offset / 2;
        }
    od
}   }


inline attacker_updates_knowledge( e )
{   atomic {

    bool     leaves = true;
    unsigned offset : X_BITS = NODE;
    unsigned width  : X_BITS = TREE + 1;
    do
    :: width == 0 -> break
    :: width != 0 -> d_step
        {
            width = width / 2;
            for ( i : 0 .. width - 1 )
            {
                j = offset + i;
                if
                :: leaves -> skip
                :: else ->
                    unsigned childL : X_BITS = j * 2 + 1;
                    unsigned childR : X_BITS = j * 2 + 2;
                    // Check current epoch for existance of subtrees
                    bool existanceOfSubtree;
                    existance_of_subtree( e, childL);
                    bool voidL = !existanceOfSubtree;
                    existance_of_subtree( e, childR);
                    bool voidR = !existanceOfSubtree;
                    // Check previous epoch for knowledge of subtrees
                    bool knowledgeOfSubtree;
                    knowledge_of_subtree( e, childL);
                    bool knowsL = knowledgeOfSubtree;
                    knowledge_of_subtree( e, childR);
                    bool knowsR = knowledgeOfSubtree;
                    
                    if
                    ::  voidL &&  voidR                       -> attackerKnowledge[e].node[j] =        Void
                    :: !voidL &&  voidR &&             knowsR -> attackerKnowledge[e].node[j] =   KnownRefs
                    :: !voidL &&  voidR &&            !knowsR -> attackerKnowledge[e].node[j] = UnknownRefs
                    ::  voidL && !voidR &&  knowsL            -> attackerKnowledge[e].node[j] =   KnownRefs
                    ::  voidL && !voidR && !knowsL            -> attackerKnowledge[e].node[j] = UnknownRefs
                    :: !voidL && !voidR &&  knowsL &&  knowsR -> attackerKnowledge[e].node[j] =   KnownNode
                    :: !voidL && !voidR && !knowsL &&  knowsR -> attackerKnowledge[e].node[j] =   KnownNode
                    :: !voidL && !voidR &&  knowsL && !knowsR -> attackerKnowledge[e].node[j] =   KnownNode
                    :: !voidL && !voidR && !knowsL && !knowsR -> attackerKnowledge[e].node[j] = UnknownNode
                    fi
                fi
            };
            offset = offset / 2;
            leaves = false;
        }
    od
    }
}


inline existance_of_subtree( i, j )
{
    existanceOfSubtree = attackerKnowledge[i].node[j] != Void
}


inline knowledge_of_subtree( i, j )
{
    knowledgeOfSubtree = attackerKnowledge[i].node[j] == KnownNode || attackerKnowledge[i].node[j] == KnownRefs
}


/********
    *
    * Oracles available to the attacker:
    *
    *   - Corrupt
    *   - Hoard
    *   - Reveal
    *
********/


inline corrupt( memberID )
{   atomic {

    printf("\n> > >\n> CGKA: Game Move = COR %d\n> > >\n", memberID);

    // Learn the secret material of the user in their current epoch
    unsigned upperBound : T_BITS = epoch;
    unsigned lowerBound : T_BITS = upperBound;

    // Learn any additional secrets they have hoarded!
    unsigned epochSavedFrom : T_BITS = hoarding[memberID];
    if
    :: epochSavedFrom < upperBound -> lowerBound = epochSavedFrom
    :: else
    fi

    printf("Corrupting from: %d -- %d\n", lowerBound, upperBound);

    // For each epoch which the member has secrets
    // (this implies that the user was a member)
    // Then the attacker learns the secrets on the direct path
    // between the member and the root node on the LBBT.
    unsigned peek : T_BITS;
    for ( peek : lowerBound .. upperBound )
    {
        if
        :: !(membership[memberID]) -> skip
        :: else ->
            printf("Passed membership guard!\n");
            unsigned off   : X_BITS = NODE;
            unsigned level : X_BITS = TREE + 1;
            do
            :: level == 0 -> break
            :: level != 0 -> d_step
                {
                    level = level / 2;
                    k = off+memberID;
                    printf("tree level: %d @ %d\n", level, j);
                    if
                    :: attackerKnowledge[peek].node[k] == UnknownNode -> attackerKnowledge[peek].node[k] = KnownNode
                    :: attackerKnowledge[peek].node[k] == UnknownRefs -> attackerKnowledge[peek].node[k] = KnownRefs
                    :: else
                    fi
                    off = off / 2;
                }
                printf("Check Index Post: %d\n", peek);
                if
                :: i == upperBound ->
                    printf("UPPER BOUNDed %d\n", peek);
                    attacker_updates_knowledge( peek );
                :: else ->
                    printf("Lower Pre %d\n", peek);
                    attacker_copy_epoch_knowledge( peek  );
                    printf("Lower Post %d\n", peek);
                    attacker_updates_knowledge(   peek+1 );
                fi
                printf("Before hand off: %d\n", peek);
            od
        fi
    }
    attacker_insight_check( epoch );
    unsafe[memberID] = true;
}   }


inline hoard( memberID )
{
    d_step {
        printf("\n> > >\n> CGKA: Game Move = HOD %d\n> > >\n", memberID);
        hoarding[memberID] = epoch
    }
}


inline reveal()
{
    d_step {
        printf("\n> > >\n> CGKA: Game Move = RVL %d\n> > >\n", epoch);
        challenge[epoch] = true;
        attackerKnowledge[epoch].node[ROOT] -> KnownNode;
    }
}


/********
    *
    * Oracles available to the Group Members:
    *
    *   - Insert Member ( ADD )
    *   - Remove Member ( RMV )
    *   - Oblige Update ( UPD )
    *
********/


// Precondition: joiner is not in the group!
inline insert_member( sender, joiner )
{
    d_step {
        printf("\n> > >\n> CGKA: Game Move = ADD %d %d\n> > >\n", sender, joiner);
        assert(sender < N);
        assert(joiner < N);
        propogate( sender, joiner, NONE );
    }
    broadcast( epoch, sender, joiner );
}


// Precondition: exiledMemeber is in the group!
inline remove_member( sender, exiled )
{
    d_step {
        printf("\n> > >\n> CGKA: Game Move = RMV %d %d\n> > >\n", sender, exiled);
        assert(sender < N);
        assert(exiled < N);
        unsafe[exiled] = false;
        propogate( sender, NONE, exiled);
    }
    broadcast( epoch, sender, exiled );
}


inline oblige_update( sender )
{
    d_step {
        printf("\n> > >\n> CGKA: Game Move = UPD %d\n> > >\n", sender);
        assert(sender < N);
        unsafe[sender] = false;
        propogate( sender, NONE, NONE );
    }
    broadcast( epoch, sender, NONE );
}


/********
    *
    * Attacker moves interacting with oracles:
    *
    *   - play_move_with_commitment
    *   - play_move_without_commitment
    *
********/


inline play_move_with_commitment()
{
    unsigned exiledID : N_BITS, banisherID : N_BITS, updaterID : N_BITS;
    
    atomic
    {
        select_updater(  forcedPlay );
        select_exiled(   forcedPlay );
        select_banisher( exiledID   );
    };

    d_step
    {
        printf("\nEpoch (t) = %d\n", epoch );
        print_global_state()
        printf("\n\texiledID    \t%d",   exiledID );
        printf("\n\tbanisherID  \t%d", banisherID );
        printf("\n\tupdaterID   \t%d",  updaterID );
        printf("\nCOMMITTING!\n");
    }

    do
    // Update
    :: updaterID != NONE ->
        oblige_update( updaterID ); break

    // Remove
    :: !groupDyad && exiledID != NONE && banisherID != NONE ->
        remove_member( banisherID, exiledID ); break

    // Insert
    :: !groupFull && !forcedPlay -> atomic
        {
            unsigned joinerID : N_BITS, senderID : N_BITS;
            select_sender();
            select_joiner();
            insert_member( senderID, joinerID );
        }; break
    od

    after_move_refresh( epoch + 1);
}


/****
  *
  * Attacker moves without advancing the epoch via one of the following oracles:
  *
  *   - Corrupt
  *   - Hoard
  *   - Reveal
  *
****/
inline play_move_without_commitment()
{   atomic
    {
        unsigned corruptedID : N_BITS, hoarderID : N_BITS;
    
        atomic
        {
            select_corrupted();
            select_hoarder();
        };
    
        d_step
        {
            printf("\nEpoch (t) = %d\n", epoch);
            print_global_state()
            printf("\n\tcanRevealKey\t%d",  revealRoot );
            printf("\n\tcorruptedID \t%d", corruptedID );
            printf("\n\thoarderID   \t%d",   hoarderID );
            printf("\nNON-Committal!\n");
        }

        do
        :: corruptedID != NONE && !forcedPlay -> corrupt( corruptedID ); break
        :: hoarderID   != NONE                -> hoard(     hoarderID ); break
        :: revealRoot                         -> reveal(              ); break
        od
    
        after_move_refresh( epoch );
    }
}


/********
    *
    * Initialization inline routines, in order of execution:
    *
    *   - Initialize
    *   - Select Group
    *   - Create Group
    *   - CGKA Security Game
    *
********/


inline CGKA_initialize()
{   atomic {
    d_step
    {
        printf("\n***********************\n* CGKA: Initialize!   *\n***********************\n");

        unsigned n : N_BITS;
        for( n : 0 .. N - 1 )
        {
            hoarding[n]  = NONE;
        };
    
        unsigned t : T_BITS;
        for( t : 0 .. T - 1 )
        {
            challenge[t] = false;
        };

        epoch     = 0;
        unsafeIDs = 0;
        
        concludedCGKA = false;
        triviality    = false; 

        attacker_initializes_knowledge()
    };

}   }


inline CGKA_create_group()
{
    d_step
    {
        // Number of members to add
        unsigned sample : N_BITS;
        select ( sample : 2 .. N );
        unsigned n      : N_BITS;
        for( n : 0 .. N - 1 )
        {
            membership[i] = n < sample;
        };

        printf("\n***********************\n* CGKA: Create Group! *\n***********************\n");

        // Set the "lead" byte to be the first member in the group.
        unsigned id0 : N_BITS = 0;
        unsigned t   : T_BITS = 0;
        print_membership();
        propogate ( id0, NONE, NONE );
        broadcast ( t,    id0, NONE );
    }
}


inline CGKA_security_game()
{
    printf("\n***********************\n* CGKA: Begin Play!   *\n***********************\n");


    // Each time the attacker takes a turn, they must decide whether or not to:
    //
    //   1. End the game; under the assumption that the attacker has won.
    //   2. Play a move which will *commit* the group members to advance to the next epoch
    //   3. Play a move which where the group members remain in the current epoch
    //
    // We call selection the options "challenge," "commitment," and "non-committal" moves, respectively.
    //
    // NOTE: option (1), is implicitly the last move in the model

    bool commitmentRequired = false;

    // Loop through all epochs
    for ( epoch : 0 .. LAST_EPOCH)
    {

        do
        // 1. Play the Challenge Move
        //     The attacker ending the game is implicitly the last move of the model
        //     so it always happens in the last epoch.
        :: epoch == LAST_EPOCH -> break
        
        // 2. Play a Commitment Move
        //     The attacker *may* play a move which commits to a new epoch...
        //     unless it is the last epoch.
        :: epoch != LAST_EPOCH -> play_move_with_commitment(); break
        
        // 3. Play a Non-commital Move
        //     The attacker *may* play a move and remain in the same epoch...
        //     unless the attacker has exhausted all indempotent non-comittal moves!
        :: !(commitmentRequired) -> play_move_without_commitment()
        od;

        // After the operation is complete, check to see if the an endgame condition has been reached.
        printf("\nLOOP broken: %d", epoch);
        printf ("\n< < <\n< Moves:   %d\n< Unsafe:  %d\n< < < \n", LAST_EPOCH - epoch, unsafeIDs);
    }
}


init
{
    CGKA_initialize();
    CGKA_create_group();
    CGKA_security_game();
    concludedCGKA = true;
}

//ltl trivial_safety { [](triviality <-> attackerKnowsRootKey) }
ltl finished_loop  { <>concludedCGKA }
//ltl attendees_more_than_one { [](attendees > 1) }
//ltl attendees_absentees_sum { [](attendees + absentees == N) }
